%***********************************************************************    
%       Este archivo resuelve el modelo de planificador central
%                       de Benigno et.al (2012)
%***********************************************************************
%***********************************************************************
%        Declaracion de variables endógenas en el modelo
%***********************************************************************

var  CT BB CN PN UU_2 UU_1 LL YN YT HN HH HT C AT;


varexo e_at ;
%***********************************************************************
%                            Parámetros
%***********************************************************************


parameters beta phi phin delta rho omega omegan kappa rho_at
          alphan alphat RR AN at_s;


beta=0.9817;
phi=0.415;
phin=(1-phi)/phi;
delta=2;
rho=2;
omega=0.3526;
omegan=(1-omega)/omega;
RR=1.01587;
kappa=0.760;
rho_at=0.537;
alphan=1-0.66;
alphat=1-0.66;
at_s=1;
AN=1;

%----------------------------------------------------------------
% 3. Model
%----------------------------------------------------------------

model;



%%***********************************************************************
% Ecuaciones del modelo
%%**********************************************************************

%%***********************************************************************
% EQ1: Balanza de Pagos
%%**********************************************************************

CT=YT+BB(-1)*RR-BB;

%%***********************************************************************
% EQ2: restricción de crédito
%%**********************************************************************

BB=-(1-phi)/phi*((AT*(HT^(1-alphat)))+((((1-omega)/omega)^(1/kappa))*((AN*HN^(1-alphan))^(1-1/kappa))/(CT^(-1/kappa))));

%%***********************************************************************
% EQ3: Equilibrio en el sector no transable, 
%%**********************************************************************

CN=YN;

%%***********************************************************************
% EQ4: Precio de no transables
%%**********************************************************************

PN=((((1-omega)/omega)^(1/kappa))*((AN*HN^(1-alphan))^(-1/kappa))/(CT^(-1/kappa)));

%%***********************************************************************
% EQ5: Ecuacion de Euler 
%%**********************************************************************

UU_1=beta*RR*UU_1(1)+LL;

%%***********************************************************************
% EQ6: consumo de no transables
%%**********************************************************************

(C-HH^(delta)/delta)^(-rho)*(1-omega)^(1/kappa)*(CN)^(-1/kappa)*(C)^(1/kappa)=UU_2;

%%***********************************************************************
% EQ7: funcion de produccion sector no transable 
%%**********************************************************************

YN=AN*HN^(1-alphan);

%%***********************************************************************
% EQ8: funcion de produccion sector  transable 
%%**********************************************************************

YT=AT*HT^(1-alphat);

%%***********************************************************************
% EQ9: Consumo en el sector transable 
%%**********************************************************************

(C-HH^(delta)/delta)^(-rho)*(omega)^(1/kappa)*(CT)^(-1/kappa)*(C)^(1/kappa)=UU_1-LL/kappa*phin*omegan*((omegan*CT)^((1-kappa)/kappa))*((AN*(HN^(1-alphan)))^((kappa-1)/kappa));

%%***********************************************************************
% EQ10: Equilibrio en el mercado de trabajo 
%%**********************************************************************

HH=HN+HT;

%%***********************************************************************
% EQ11: Definicion de consumo agregado 
%%**********************************************************************

C=(omega^(1/kappa)*(CT)^((kappa-1)/kappa)+(1-omega)^(1/kappa)*(CN)^((kappa-1)/kappa))^(kappa/(kappa-1));

%%***********************************************************************
% EQ12: DINÁMICA DE LA PRODUCTIVIDAD DE NO TRANSABLES
%%**********************************************************************

log(AT)=rho_at*log(AT(-1))+e_at;

%%***********************************************************************
% EQ13: TRABAJO DE NO TRANSABLE
%%**********************************************************************
(C-HH^(delta)/delta)^(-rho)*(HH^(delta-1))=UU_2*(1-alphan)*AN*(HN^(-alphan))+LL*phin*(1-alphan)*(AN^((kappa-1)/kappa))*(HN^((1-alphan)*(((kappa-1)/kappa)-1)))*(omegan^(1/kappa))*(CT^(1/kappa))*((kappa-1)/kappa);

%%***********************************************************************
% EQ14: TRABAJO EN EL SECTOR TRANSABLE
%%**********************************************************************
(C-HH^(delta)/delta)^(-rho)*(HH^(delta-1))=UU_1*(1-alphat)*AT*(HT^(-alphat))+LL*phin*(1-alphat)*AT*(HT^(-alphat));


end;

initval;

HN=1;
HT=1;
HH=1;
CN=1;
CT=1;
C=1;
YT=1;
YN=1;
BB=1;
LL=1;
UU_1=1;
UU_2=1;
AT=1;
PN=1;

end;

shocks;

var e_at =0.0134;

end;

steady;

stoch_simul(hp_filter = 1600, order = 1);


