%**********************************************************************       
%Este archivo resuelve el modelo RBC de una economía abierta
%***********************************************************************

%***********************************************************************
%        Declaracion de variables endogenas en el modelo
%***********************************************************************

var YY, CC, KK, INV, HH, WP, RR, AA, GG, UU_cc, DD, RP, TB, R_STAR;

%***********************************************************************
%        Declaracion de choques
%***********************************************************************

varexo ee, uu, rr;

%***********************************************************************
%        Parametros
%***********************************************************************


parameters alpha, beta, delta,sigma, vv, gama_gg, rr_ss, yy_kk,yy_hh,cc_kk
           hh_kk, kk, yy, cc, inv, gg, hh, wp,uu_cc, rho_aa, rho_gg,phi, bb
           chi, rho_rr  ;

alpha=0.3;   % Participacion del capital en el producto.
beta= 0.99;  % factor estocastico de descuento
delta=0.025;  % Tasa trimestral de depreciacion del capital
sigma=2;
vv=1.5;
gama_gg=0.15;
rho_aa=0.0;
rho_gg=0.5;
phi=0.1;
bb=0.7;
chi=0.1;
rho_rr=0.5;


%***********************************************************************
%        solucion del estado estacionario
%***********************************************************************

rr_ss=1/beta-1;
yy_kk=(1-beta*(1-delta))/(beta*alpha);
yy_hh=(yy_kk)^(-alpha/(1-alpha));
cc_kk=yy_kk*(1-gama_gg)-delta;
hh_kk=(yy_kk)^(1/(1-alpha));
kk=((1-alpha)*cc_kk^(-sigma)*yy_hh*hh_kk^(-vv))^(1/(vv+sigma));
cc=cc_kk*kk;
yy=yy_kk*kk;
inv=delta*kk;
gg=gama_gg*yy;
hh=hh_kk*kk;
wp=(1-alpha)*yy/hh;
uu_cc=(cc-bb*cc)^(-sigma);


%***********************************************************************
%        Ecuaciones del modelo
%***********************************************************************

model;

%***********************************************************************
%        EQ1: FUNCION DE PRODUCCION
%***********************************************************************

      YY= AA*KK(-1)^alpha*HH^(1-alpha);

%***********************************************************************
%        EQ2: DINAMICA DEL CAPITAL
%***********************************************************************
 
     KK=INV+(1-delta)*KK(-1);


%***********************************************************************
%        EQ3: DEMANDA DE TRABAJO
%***********************************************************************
     WP=(1-alpha)*YY/HH;

%***********************************************************************
%        EQ4: UTILIDAD MARGINAL
%***********************************************************************

     UU_cc=(CC-bb*CC(-1))^(-sigma);

%***********************************************************************
%        EQ5: ECUACION DE EULER
%***********************************************************************

  UU_cc=beta*UU_cc(1)*(1+RR);

%***********************************************************************
%        EQ6: Q TOBIN
%***********************************************************************

 1=beta*UU_cc(1)/UU_cc*(alpha*YY(1)/KK+(1-delta));

%***********************************************************************
%        EQ7: OFERTA DE TRABAJO
%***********************************************************************

    HH^vv=UU_cc*WP;

%***********************************************************************
%        EQ8: BALANZA COMERCIAL
%***********************************************************************
     TB=YY-CC-INV-GG;

%***********************************************************************
%        EQ9: BALANZA DE PAGOS
%***********************************************************************
     TB=DD-DD(-1)*(1+RR(-1));

%***********************************************************************
%        EQ10: PREMIO POR RIESGO PAIS
%***********************************************************************
    RP=chi*(exp(-DD)-1);

%***********************************************************************
%        EQ11: TASA DE INTERES
%***********************************************************************
    RR=(1+RP)*(1+R_STAR)-1;
%***********************************************************************
%        EQ12: DINAMICA DE LA PRODUCTIVIDAD
%***********************************************************************
    
    log(AA)=rho_aa*log(AA(-1))+ee;

%***********************************************************************
%        EQ13: DINAMICA DEL GASTO PUBLICO
%***********************************************************************

   log(GG)=log(gg)*(1-rho_gg)+rho_gg*log(GG(-1))+uu;

%***********************************************************************
%        EQ14: TASA DE INTERES INTERNACIONAL
%***********************************************************************
  
R_STAR=rho_rr*R_STAR(-1)+rr;

end;

initval;

YY = yy;
CC =cc;
HH =hh;
KK =kk;
WP=wp;
RR=rr_ss;
AA=1;
GG=gg;
UU_cc=uu_cc;
INV=inv;
TB=0;
DD=0;
R_STAR=0;
RP=0;
end;

steady; 
 
check; 

shocks;
var ee= 0.009;
var uu= 0.009;
var rr= 0.009;
end;

stoch_simul(solve_algo =1,order=1,ar=1,irf=40) YY CC INV TB RP DD;
