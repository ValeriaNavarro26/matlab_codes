
%***********************************************************************    
%       Este archivo resuelve el equilibrio competitivo del modelo 
%                       de Benigno et.al (2012)
%***********************************************************************

%***********************************************************************
%        Declaracion de variables endógenas en el modelo
%***********************************************************************

var  CT BB CN PN WW UU LL YN YT HN HH HT C AT;


varexo e_at ;
%***********************************************************************
%                            Parámetros
%***********************************************************************


parameters beta phi delta rho omega kappa rho_at
           lmu at_s hn_s ht_s h_s yt_s yn_s cn_s bb_s cc_s
           lambda_s ct_s uu_s ww_s pn_s alphan alphat LMU RR AN;


beta=0.9817;
phi=0.415;
delta=2;
rho=2;
omega=0.3526;
RR=1.01587;
kappa=0.760;
rho_at=0.537;
alphan=1-0.66;
alphat=1-0.66;
at_s=1;
AN=1;
%%***********************************************************************
% Solucion del Estado Estacionario
%%**********************************************************************
pn_s=1;
lmu=1-beta*RR;
hn_s=(1-omega)*((1+(1-phi)/phi*lmu)*(1-(1-phi)/phi*(RR-1)));
ht_s=(1+(1-phi)/phi*lmu)-hn_s;
hh_s=hn_s+ht_s;
yt_s=ht_s;
yn_s=hn_s;
cn_s=yn_s;
cc_s=hn_s/(1-omega);
bb_s=-(1-phi)/phi*hh_s;
ct_s=ht_s+bb_s*(RR-1);
uu_s=(cc_s-hh_s^(delta)/delta)^(-rho)*(omega)^(1/kappa)*(ct_s)^(-1/kappa)*(cc_s)^(1/kappa);
lambda_s=lmu*uu_s;
ww_s=(1-alphan)*pn_s*AN*hn_s^(-alphan);

%----------------------------------------------------------------
% 3. Model
%----------------------------------------------------------------

model;



%%***********************************************************************
% Ecuaciones del modelo
%%**********************************************************************

%%***********************************************************************
% EQ1: Balanza de Pagos
%%**********************************************************************

CT=YT+BB(-1)*RR-BB;


%%***********************************************************************
% EQ2: restricción de crédito
%%**********************************************************************

BB=-(1-phi)/phi*(YT+PN*YN);

%%***********************************************************************
% EQ3: Equilibrio en el sector no transable, 
%%**********************************************************************

CN=YN;

%%***********************************************************************
% EQ4: Precio de no transables, 
%%**********************************************************************

PN=(1-alphan)*AT*(HT^(-alphat))/((1-alphan)*AN*(HN^(-alphan)));

%%***********************************************************************
% EQ5: Salario Real, 
%%**********************************************************************


WW=(1-alphan)*PN*AN*(HN^(-alphan));

%%***********************************************************************
% EQ6: Ecuacion de Euler 
%%**********************************************************************

UU=beta*RR*UU(1)+LL;

%%***********************************************************************
% EQ7: Ecuacion de Euler 
%%**********************************************************************

UU=(C-HH^(delta)/delta)^(-rho)*(omega)^(1/kappa)*(CT)^(-1/kappa)*(C)^(1/kappa);

%%***********************************************************************
% EQ8: funcion de produccion sector no transable 
%%**********************************************************************

YN=AN*HN^(1-alphan);

%%***********************************************************************
% EQ9: funcion de produccion sector  transable 
%%**********************************************************************

YT=AT*HT^(1-alphat);

%%***********************************************************************
% EQ10: Consumo en el sector no transable 
%%**********************************************************************

PN=(1-omega)^(1/kappa)*(CN)^(-1/kappa)/((omega)^(1/kappa)*(CT)^(-1/kappa));

%%***********************************************************************
% EQ11: Demanda de trabajo 
%%**********************************************************************

HH^(delta-1)=(omega*C/CT)^(1/kappa)*WW*(1+(1-phi)/phi*LL/UU);

%%***********************************************************************
% EQ12: Equilibrio en el mercado de trabajo 
%%**********************************************************************

HH=HN+HT;


%%***********************************************************************
% EQ13: Definicion de consumo agregado 
%%**********************************************************************

C=(omega^(1/kappa)*(CT)^((kappa-1)/kappa)+(1-omega)^(1/kappa)*(CN)^((kappa-1)/kappa))^(kappa/(kappa-1));


log(AT)=rho_at*log(AT(-1))+e_at;

end;

initval;

HN=hn_s;
HT=ht_s;
HH=hh_s;
CN=cn_s;
CT=ct_s;
C=cc_s;
YT=yt_s;
YN=yn_s;
BB=bb_s;
LL=lambda_s;
UU=uu_s;

AT=at_s;
WW=ww_s;
PN=pn_s;

end;

shocks;

var e_at =0.0134;

end;

steady;

stoch_simul(hp_filter = 1600, order = 1);

